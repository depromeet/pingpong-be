plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'java'
    id "com.epages.restdocs-api-spec" version "0.16.2"
    id 'org.hidetake.swagger.generator' version '2.18.2'
}

group 'com.dpm.winwin.api'
version '0.0.1-SNAPSHOT'

ext {
    snippetsDir = file('build/generated-snippets')
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExtensions
}

repositories {
    mavenCentral()
}
swaggerSources {
    sample {
        setInputFile(file("${project.buildDir}/api-spec/openapi3.json"))
    }
}

dependencies {
    implementation project(":winwin-be-common")
    implementation project(":winwin-be-domain")

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'com.epages:restdocs-api-spec:0.16.2'
    implementation "org.springdoc:springdoc-openapi-ui:1.6.11"
    implementation 'com.epages:restdocs-api-spec-mockmvc:0.16.2'
    implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'
    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'org.springframework.security:spring-security-test'
    implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.2'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.2'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.2'
    swaggerUI 'org.webjars:swagger-ui:4.15.0'
}

openapi3 {
    title = 'PINGPONG-BE API DOCUMENTATION'
    servers = [{ url = "http://localhost:8080" }, { url = "https://localhost:8080" }]
}
/**
 * REST Docs
 */
test {
    useJUnitPlatform()
    outputs.dir snippetsDir
}

asciidoctor {
    dependsOn test
    inputs.dir snippetsDir
    configurations 'asciidoctorExtensions'

    baseDirFollowsSourceFile()
}

bootJar {
    dependsOn asciidoctor
    dependsOn 'copySwaggerUI'
}


// 3.4. Task 및 설정 추가
// 3.4.1
// GenerateSwaggerUI 태스크가, openapi3 task 를 의존하도록 설정
tasks.withType(GenerateSwaggerUI) {
    dependsOn 'openapi3'
}

// 3.4.2
// 생성된 SwaggerUI 를 jar 에 포함시키기 위해 build/resources 경로로 로 복사
tasks.register('copySwaggerUI', Copy) {
    dependsOn 'generateSwaggerUISample'

    def generateSwaggerUISampleTask = tasks.named('generateSwaggerUISample', GenerateSwaggerUI).get()

    from("${generateSwaggerUISampleTask.outputDir}")
    into("${project.buildDir}/resources/main/static/docs")
}
